/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    IMPORT MODULES / SUBWORKFLOWS / FUNCTIONS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/
include { FASTQC                 } from '../modules/nf-core/fastqc/main'
include { MULTIQC                } from '../modules/nf-core/multiqc/main'

include { DECONTAM   } from '../subworkflows/local/decontam'
include { TAXONOMY   } from '../subworkflows/local/taxonomy'
include { FUNCTIONAL } from '../subworkflows/local/functional'

include { paramsSummaryMap       } from 'plugin/nf-schema'
include { paramsSummaryMultiqc   } from '../subworkflows/nf-core/utils_nfcore_pipeline'
include { softwareVersionsToYAML } from '../subworkflows/nf-core/utils_nfcore_pipeline'
include { methodsDescriptionText } from '../subworkflows/local/utils_nfcore_funomic_pipeline'

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    RUN MAIN WORKFLOW
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

workflow FUNOMIC {

    take:
    ch_samplesheet // channel: samplesheet read in from --input

    main:
    ch_versions      = channel.empty()
    ch_multiqc_files = channel.empty()

    //
    // MODULE: FastQC on raw reads
    //
    FASTQC(ch_samplesheet)
    ch_multiqc_files = ch_multiqc_files.mix(FASTQC.out.zip.collect { it[1] })

    //
    // SUBWORKFLOW: Remove bacterial reads (Bowtie2 → UHGG database)
    //              Outputs unmapped (fungal-enriched) reads as fastq.gz pairs
    //
    DECONTAM(ch_samplesheet)
    ch_multiqc_files = ch_multiqc_files.mix(DECONTAM.out.mqc)
    ch_clean_reads = DECONTAM.out.reads

    //
    // SUBWORKFLOW: Taxonomic profiling of fungal reads (FunOMIC-T database)
    //              Bowtie2 → q30 filter → coverage filter → idxstats → buglist
    //
    TAXONOMY(ch_clean_reads)
    ch_buglist = TAXONOMY.out.buglist

    //
    // SUBWORKFLOW: Functional profiling of fungal reads (FunOMIC-P database)
    //              DIAMOND blastx → filter → pathway abundance tables
    //
    // FUNCTIONAL(ch_clean_reads)

    //
    // Collate and save software versions
    //
    def topic_versions = Channel.topic("versions")
        .distinct()
        .branch { entry ->
            versions_file: entry instanceof Path
            versions_tuple: true
        }

    def topic_versions_string = topic_versions.versions_tuple
        .map { process, tool, version ->
            [ process[process.lastIndexOf(':')+1..-1], "  ${tool}: ${version}" ]
        }
        .groupTuple(by: 0)
        .map { process, tool_versions ->
            tool_versions.unique().sort()
            "${process}:\n${tool_versions.join('\n')}"
        }

    softwareVersionsToYAML(ch_versions.mix(topic_versions.versions_file))
        .mix(topic_versions_string)
        .collectFile(
            storeDir: "${params.outdir}/pipeline_info",
            name:     'nf_core_funomic_software_mqc_versions.yml',
            sort:     true,
            newLine:  true
        ).set { ch_collated_versions }

    //
    // MODULE: MultiQC
    //
    ch_multiqc_config = Channel.fromPath(
        "$projectDir/assets/multiqc_config.yml",
        checkIfExists: true
    )

    ch_multiqc_custom_config = params.multiqc_config \
        ? Channel.fromPath(params.multiqc_config, checkIfExists: true)
        : Channel.empty()

    ch_multiqc_logo = params.multiqc_logo \
        ? Channel.fromPath(params.multiqc_logo, checkIfExists: true)
        : Channel.empty()

    summary_params      = paramsSummaryMap(workflow, parameters_schema: "nextflow_schema.json")
    ch_workflow_summary = Channel.value(paramsSummaryMultiqc(summary_params))

    ch_multiqc_custom_methods_description = params.multiqc_methods_description \
        ? file(params.multiqc_methods_description, checkIfExists: true)
        : file("$projectDir/assets/methods_description_template.yml", checkIfExists: true)

    ch_methods_description = Channel.value(
        methodsDescriptionText(ch_multiqc_custom_methods_description)
    )

    ch_multiqc_files = ch_multiqc_files.mix(
        ch_workflow_summary.collectFile(name: 'workflow_summary_mqc.yaml')
    )

    ch_multiqc_files = ch_multiqc_files.mix(ch_collated_versions)

    ch_multiqc_files = ch_multiqc_files.mix(
        ch_methods_description.collectFile(
            name: 'methods_description_mqc.yaml',
            sort: true
        )
    )

    def mqc_config = [ file("$projectDir/assets/multiqc_config.yml", checkIfExists: true) ]
    if (params.multiqc_config) {
        mqc_config << file(params.multiqc_config, checkIfExists: true)
    }

    def mqc_logo = params.multiqc_logo ? file(params.multiqc_logo, checkIfExists: true) : []

    MULTIQC (
        ch_multiqc_files.collect().map { files ->
            [
                [ id: 'multiqc' ], // meta
                files,             // multiqc_files
                mqc_config,        // multiqc_config
                mqc_logo,          // multiqc_logo
                [],                // replace_names
                []                 // sample_names
            ]
        }
    )

    emit:
    multiqc_report = MULTIQC.out.report.toList()
    versions       = ch_versions
}

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    THE END
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/
